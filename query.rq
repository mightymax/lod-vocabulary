PREFIX dct: <http://purl.org/dc/terms/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?item ?itemType (str(sample(?labels)) as ?label) (str(sample(?descriptions)) as ?description)  WHERE {
  graph <$GRAPH> {
  { 
    ?item a/rdfs:subClassOf* ?class .
    bind ('class' as ?itemType)
    filter(?class in ( owl:Class, rdfs:Class ))
  }
  union
  { 
    ?item a/rdfs:subClassOf* ?class .
    bind ('property' as ?itemType)
    filter(?class in ( owl:ObjectProperty, owl:AnnotationProperty, owl:DatatypeProperty, rdf:Property, rdfs:Datatype ))
  }
  optional {
    ?item ?labelType ?labels
    filter(langmatches(lang(?labels),'en') || lang(?labels) = '')
  }

  optional {
    ?item  ?descriptionType ?descriptions
    filter(langmatches(lang(?descriptions),'en') || lang(?descriptions) = '')
  }
  filter(!bound(?descriptionType) || ?descriptionType in (dct:description, rdfs:comment, skos:definition, skos:scopeNote))
  filter(!bound(?labelType) || ?labelType in (rdfs:label, skos:prefLabel, dct:title))
} }
group by ?item ?itemType
